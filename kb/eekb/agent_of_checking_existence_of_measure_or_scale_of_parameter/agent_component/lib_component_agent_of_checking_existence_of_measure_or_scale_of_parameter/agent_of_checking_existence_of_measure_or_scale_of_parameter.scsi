agent_of_checking_existence_of_measure_or_scale_of_parameter
=> nrel_main_idtf:
	[агентная scp-программа проверки наличия единицы измерения или шкалы у параметра] (* <- lang_ru;; *);
	[agent scp-program of checking existence of measure or scale of parameter] (* <- lang_en;; *);
	<- agent_scp_program;;

scp_program -> agent_of_checking_existence_of_measure_or_scale_of_parameter
	(*
	-> rrel_params: .agent_of_change_arc_direction_params 
		(*
		-> rrel_1: rrel_in: _event;;
		-> rrel_2: rrel_in: _input_arc;;
		*);;

	-> rrel_operators: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_set 
		(*
		-> rrel_init: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1A
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_assign: rrel_scp_var: _temp;;
			-> rrel_2: rrel_fixed: rrel_scp_var: _input_arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1B;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1B
			(*
		  	<- searchElStr3;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: action_to_check_existence_of_measure_or_scale_of_parameter;;
		  	-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
		  	-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

		  	=> nrel_then: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1C;;
		  	=> nrel_else: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7B;;
			*);;
		
		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator1C
			(*
		  	<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_struct: _param;;

		  	=> nrel_then: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2A;;
		  	=> nrel_else: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_call_proc_of_adding_validating_message;;
			*);;	

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2A
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: non_atomic_section;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _param;;

			=> nrel_then: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2C;;
			=> nrel_else: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2B;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2B
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: atomic_section;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _param;;

			=> nrel_then: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2C;;
			=> nrel_else: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7B;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2C
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_const: rrel_scp_var: _answer_of_checking_parameter;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2D;;
			*);;	

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator2D
			(*
			<- genEl;;

			-> rrel_1: rrel_assign: rrel_node: rrel_scp_var: _set_of_parameter;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3A;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3A
			(*
			<- searchSetStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _param;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: rrel_node: _node1;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _set_of_nodes_in_struct;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3B;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3B
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_nodes_in_struct;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_assign: rrel_scp_var: _concrete_parameter;;

			=> nrel_then: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3C;;
			=> nrel_else: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator4A;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3C
			(*
			<- eraseEl;;

			-> rrel_1: rrel_erase: rrel_fixed: rrel_pos_const_perm: rrel_scp_var: _arc1;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3D;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3D
			(*
			<- searchElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: parameter;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _concrete_parameter;;

			=> nrel_then: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3E;;
			=> nrel_else: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3B;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3E
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_parameter;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _concrete_parameter;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator3B;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator4A
			(*
			<- genEl;;

			-> rrel_1: rrel_node: rrel_assign: rrel_const: rrel_scp_var: rrel_struct: _answer_of_proc_of_checking_parameter;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5A;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5A
			(*
			<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_checking_existence_of_measure_or_scale_of_parameter;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator5A_params 
		  		(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _set_of_parameter;;
                		-> rrel_2: rrel_fixed: rrel_scp_var: _answer_of_proc_of_checking_parameter;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr1;;

		  	=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator6A	
			(*
			<- waitReturn;;

			-> rrel_1: rrel_fixed: rrel_scp_var: _descr1;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_gen_agent_work_result;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_gen_agent_work_result
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_generation_of_verification_answer;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_gen_agent_work_result_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
				-> rrel_2: rrel_fixed: rrel_scp_const: action_to_check_existence_of_measure_or_scale_of_parameter;;
				-> rrel_3: rrel_fixed: rrel_scp_var: _answer_of_proc_of_checking_parameter;;
				-> rrel_4: rrel_fixed: rrel_scp_const: struct_of_checking_existence_of_measure_or_scale_of_parameter;;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr2;;

		  	=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_waitreturn_generation_agent_work_result;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_waitreturn_generation_agent_work_result
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr2;;

		  	=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A;;
			*);;	

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7A
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: question_finished_successfully;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_return;; 
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator7B
			(*
			<- genElStr3;;

			-> rrel_1: rrel_fixed: rrel_scp_const: question_finished_unsuccessfully;;
			-> rrel_2: rrel_assign: rrel_pos_const_perm: rrel_scp_var: _arc1;;
			-> rrel_3: rrel_fixed: rrel_scp_var: _quest;;

			=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_return;; 
			*);;


/**************************************************************************Wrong Parametrs************************************************************/

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_call_proc_of_adding_validating_message 
			(*
		  	<- call;;

		  	-> rrel_1: rrel_fixed: rrel_scp_const: proc_of_adding_validating_message;;
		  	-> rrel_2: rrel_fixed: rrel_scp_const: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_call_proc_of_adding_validating_message_params 
				(*
				-> rrel_1: rrel_fixed: rrel_scp_var: _quest;;
				-> rrel_2: rrel_fixed: rrel_scp_const: [Wrong parameter!It must be structure];;
				*);;
		  	-> rrel_3: rrel_assign: rrel_scp_var: _descr3;;

		  	=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_waitreturn_proc_of_adding_validating_message;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_waitreturn_proc_of_adding_validating_message
			(*
		  	<- waitReturn;;

		  	-> rrel_1: rrel_fixed: rrel_scp_var: _descr3;;

		  	=> nrel_goto: .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_return;;
			*);;

		-> .agent_of_checking_existence_of_measure_or_scale_of_parameter_operator_return 
			(*
		  	<- return;;
			*);;
		*);;
	*);;
